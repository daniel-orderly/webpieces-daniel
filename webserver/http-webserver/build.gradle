apply plugin: 'jacoco' //code coverage
apply plugin: 'idea'

sourceSets {
    test {
        resources {
            //This ADDS src/test/java/**/*.html and we still read in src/test/resources/**
            srcDirs = ["src/test/java", "src/test/resources"]
            excludes = ["**/*.java"]
        }
    }
}

//Intellij Idea has a weird quirk we need to avoid
if (System.getProperty('idea.active')) {

    // to have classes and resources in the same output directory
    idea {
        module {
            testOutputDir file("out/production/classes")
        }
    }
}

//ok, this is the deal here.  JPA/hibernate made the decision to look for a persistence.xml file
//and scan for classes with @Entity in the directory OR jar with that xml file only
//maven(and I hate this) a long time ago separated src/main/java and src/main/resources but
//this screws tools in many situations like this one so this puts it back so the output is at
//least put to the same location
//sourceSets.test.output.resourcesDir = sourceSets.test.output.classesDir

task copyPersistenceXml(type: Copy, dependsOn: 'compileTestJava') {
    from 'src/test/resources/META-INF'
    into 'output/classes/java/test/META-INF'
    include 'persistence.xml'
    //outputs.upToDateWhen { false }
}

test.dependsOn ("copyPersistenceXml")

dependencies {

    api deps['guice']
    implementation deps['acme-sslcerts']
    api project(':http:http-frontend2')
    api project(':webserver:http-templating')
    api project(':webserver:http-router')

    testImplementation project(':core:core-mock')
    testImplementation project(':core:core-ddl')
    testImplementation project(':core:runtimecompile')
    testImplementation project(':webserver:http-webserver-test')
    testImplementation project(':webserver:http-router-dev')
    testImplementation project(':webserver-plugins:plugin-hibernate')
    testImplementation project(':webserver-plugins:plugin-json-jackson')

    testImplementation deps['h2db']

    testImplementation 'commons-io:commons-io'
    testImplementation 'jakarta.validation:jakarta.validation-api'
    testImplementation deps['selenium']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = 'The full webpieces server AS A library'
            }
        }
    }
}


test {
    if (!project.hasProperty('includeSelenium')) {
        exclude '**/*Selenium*'
    }
}
