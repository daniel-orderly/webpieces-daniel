plugins {
    id 'com.gradle.build-scan' version '1.0'
    id "com.dorongold.task-tree" version "1.2.2"
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

allprojects {
   apply plugin: 'java'

   //We could do this just for the template projects, BUT during debugging, it is sooo
   //damn nice to have the var names, it is worth the performance penalty I think though
   //we could change this
   [compileJava, compileTestJava]*.options.collect {options ->
     options.compilerArgs.add '-parameters'
   }

   buildDir = file('output')
   project.ext {
      stagingDirName = 'webpieces-webserver'
      outputStagingDir = new File(buildDir, stagingDirName) 
   }
}

subprojects {
   apply plugin: 'eclipse'
   apply plugin: 'idea'

   //This is an input directory that we read jars from if they are not in a repo somewhere
   project.ext.fixedLibDir = file('lib')
   project.ext.toStagingDir = file(new File(buildDir, 'alljars'))
   archivesBaseName = "webpieces-${it.name}"

   repositories {
       mavenCentral()
   }
   dependencies {
       testCompile 'junit:junit:4.11'
       //for logging in tests
       testCompile 'org.codehaus.groovy:groovy-all:2.4.6'

       compile 'javax.inject:javax.inject:1'
       compile 'org.slf4j:slf4j-api:1.7.21'
       //do we need this one..
       //compile 'ch.qos.logback:logback-core:1.1.7'
       compile 'ch.qos.logback:logback-classic:1.1.7'
       compile 'joda-time:joda-time:2.9.4'

       //This line includes any jars a developer drops into {project}/lib directory
       compile fileTree(dir: 'lib', include: '*.jar')
   }

   //This is really only needed for devrouter, embeddablewebserver, http-client, and embeddablehttpproxy
   //I wonder if we can list out those few projects and this task to each one and the assemble.dependsOn as well
   //let's at least copy all jars to a single directory to use in creating an application...
   //ALSO, this DELETES jars we no longer use so it has to be different directory than the dest jar we are creating as well
   task syncJars(type: Sync, dependsOn: 'jar') {
       from(configurations.compile) {}
       from(fixedLibDir) {}
       from(libsDir) {}
       into toStagingDir
   }

   assemble.dependsOn('syncJars')

   test{
      beforeTest{ descr ->
        logger.warn("Starting Test ${descr.className} : ${descr.name}")
      }
   }

   eclipseProject {
      doLast {
         // https://discuss.gradle.org/t/how-to-write-properties-to-3rd-party-eclipse-settings-files/6499/2

         def props = new Properties()
         file(".settings/org.eclipse.jdt.core.prefs").withInputStream {
            stream -> props.load(stream)
         }
         props.setProperty("org.eclipse.jdt.core.compiler.codegen.methodParameters", "generate")
         file(".settings/org.eclipse.jdt.core.prefs").withOutputStream {
            stream -> props.store(stream, null)
         }
     }
   }
}

project(':core-channelmanager2') {
    dependencies {
        compile project(':core-util'), project(':core-datawrapper'), project(':core-ssl')
    }
}

project(':core-ssl') {
    dependencies {
        compile project(':core-datawrapper')
    }
}

project(':core-asyncserver') {
    dependencies {
        compile project(':core-channelmanager2')
    }
}

project(':http-parser2') {
    dependencies {
        compile project(':core-datawrapper')
    }
}

project(':http-parser1_1') {
    dependencies {
        compile project(':core-datawrapper')
    }
}

project(':http-client') {
    dependencies {
        compile project(':core-channelmanager2'), project(':http-parser1_1')
    }
}

project(':http-frontend') {
    dependencies {
        compile project(':core-asyncserver'), project(':http-parser1_1'), project(':http-client')
    }
}

project(':http-router') {
    dependencies {
        compile 'com.google.inject:guice:4.0'
        project(':http-parser1_1')
        compile project(':core-util')
    }
}

project(':http-router-dev') {
    dependencies {
        compile project(':http-router')
        compile project(':runtimecompile')
        testCompile project(':core-mock')
    }
}

project(':http-templating') {
    dependencies {
        compile 'com.google.inject:guice:4.0'
        compile project(':core-util')
        compile 'org.codehaus.groovy:groovy-all:2.4.6'
    }
}

project(':embeddablehttpproxy') {
    dependencies {
        compile 'com.google.inject:guice:4.0'
        compile 'com.google.guava:guava:19.0'
        compile project(':http-frontend'), project(':http-parser1_1'), project(':http-client')
    }
}

project(':full-httpproxy') {
    dependencies {
        compile project(':embeddablehttpproxy')
        compile 'org.codehaus.groovy:groovy-all:2.4.6' //we use groovy in logback.xml
    }
}

project(':embeddablewebserver') {
    dependencies {
        compile 'com.google.inject:guice:4.0'
        compile project(':http-frontend'), project(':http-parser1_1'), project(':http-router')
        project(':http-templating')
    }
}

project(':runtimecompile') {
    dependencies {
        compile 'org.eclipse.jdt.core.compiler:ecj:4.5.1'
        compile project(':core-util')
    }
}

project(':projectcreator') {
    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.4.6' //we use groovy in logback.xml
    }
}

project(':webpieces-webserver:templateProject:TEMPLATEAPPNAME') {
    dependencies {
        compile project(':embeddablewebserver')
    }
}

project(':webpieces-webserver:templateProject:TEMPLATEdev-server') {
    dependencies {
        compile project(':embeddablewebserver')
        compile project(':http-router-dev')
        compile project(':webpieces-webserver:templateProject:TEMPLATEAPPNAME')
    }
}

task stageTemplate(type: Copy) {
    from '.'
    into buildDir
    include stagingDirName + '/**'
    exclude stagingDirName + '/output'
    exclude stagingDirName + '/.classpath'
    exclude stagingDirName + '/.project'
    exclude stagingDirName + '/.settings'
    exclude stagingDirName + '/templateProject/.classpath'
    exclude stagingDirName + '/templateProject/.project'
    exclude stagingDirName + '/templateProject/.settings'
    exclude stagingDirName + '/templateProject/TEMPLATEAPPNAME/output'
    exclude stagingDirName + '/templateProject/TEMPLATEAPPNAME/bin'
    exclude stagingDirName + '/templateProject/TEMPLATEAPPNAME/.classpath'
    exclude stagingDirName + '/templateProject/TEMPLATEAPPNAME/.project'
    exclude stagingDirName + '/templateProject/TEMPLATEAPPNAME/.settings'
    exclude stagingDirName + '/templateProject/TEMPLATEdev-server/output'
    exclude stagingDirName + '/templateProject/TEMPLATEdev-server/bin'
    exclude stagingDirName + '/templateProject/TEMPLATEdev-server/.classpath'
    exclude stagingDirName + '/templateProject/TEMPLATEdev-server/.project'
    exclude stagingDirName + '/templateProject/TEMPLATEdev-server/.settings'
}

task stageWebServer(type: Sync, dependsOn: [':embeddablewebserver:assemble', 'stageTemplate']) {
    from childProjects.embeddablewebserver.toStagingDir
    into new File(outputStagingDir, 'lib-prod')
}

task stageDevServer(type: Sync, dependsOn: [':http-router-dev:assemble', 'stageWebServer']) {
    from childProjects['http-router-dev'].toStagingDir
    into new File(outputStagingDir, 'lib-development')
    exclude { details -> 
       def fileNames = stageWebServer.source.collect{ entry -> entry.getName()}
       fileNames.contains(details.file.getName())
    }
}

task stageProjectCreator(type: Sync, dependsOn: ':projectcreator:assemble') {
    from childProjects['projectcreator'].toStagingDir
    into new File(outputStagingDir, 'lib-creation')
}

assemble.dependsOn(['stageDevServer', 'stageProjectCreator'])


