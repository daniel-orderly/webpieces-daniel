//This file is used when we build the whole webpieces project AND is not part of template generation

publishing.publications.mavenJava.pom.description='fake project used in testing.  ignore this jar'

sourceSets {
    main {
        resources {
            srcDirs += ["src/main/java"]
            excludes = ["logback.xml"]
        }
    }
}


//Intellij Idea has a weird quirk we need to avoid
if (System.getProperty('idea.active')) {

    // to have classes and resources in the same output directory
    idea {
        module {
            outputDir file("out/production/classes")
        }
    }
}

dependencies {
    implementation project(':webserver:http-webserver')
    //we start people off with the hibernate plugin
    implementation project(':webserver-plugins:plugin-hibernate')
    implementation project(':webserver-plugins:plugin-json-jackson')
    implementation project(':webserver-plugins:plugin-backend')
    implementation project(':webserver-plugins:plugin-install-sslcert')
    implementation project(':webserver-plugins:plugin-properties')
    implementation deps['commons-lang'] //used in json escaping
    //for connection pooling: https://mvnrepository.com/artifact/org.hibernate/hibernate-hikaricp
    implementation deps['hikari-cp']
    implementation 'jakarta.validation:jakarta.validation-api'

    testImplementation project(':core:core-ddl')
    testImplementation project(':core:core-mock')
    testImplementation project(':webserver-plugins:plugin-h2db')
    testImplementation project(':webserver:http-webserver-test')
    testImplementation deps['selenium']
}

//NOTE: jar section has to go AFTER dependencies section to read all dependencies
jar {
    manifest.attributes(
            "Class-Path": configurations.runtimeClasspath.files.collect { it.name }.join(' '),
            "Main-Class": 'webpiecesxxxxxpackage.Server'
    )
}

compileJava.doLast {
   logger.warn("processed resources="+sourceSets.main.output.resourcesDir)
}

test {
    if (!project.hasProperty('includeSelenium')) {
        exclude '**/*Selenium*'
    }
}
